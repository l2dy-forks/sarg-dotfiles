#+OPTIONS: num:nil
#+SETUPFILE: ~/devel/ext/org-html-themes/org/theme-readtheorg-local.setup
* guix
To define guix dependencies add a source code block with =:noweb-ref guix-pkg :tangle no :exports none= as header arguments. It'll be then tangled to =~/.dotfiles/guix/emacs-packages.scm= and merged with the main =home-environment=.

#+begin_src scheme :noweb yes :tangle ~/.dotfiles/guix/emacs-home.scm
(use-modules
 (gnu packages)
 (gnu home))
(home-environment
 (services (list
            <<guix-srv>>
            ))
 (packages
  (map (compose list specification->package+output)
       '("emacs-next"
         "emacs-emacsql"
         "emacs-pdf-tools"
         "emacs-telega"
         "emacs-telega-contrib"
         "emacs-vterm"
         "avfs"
         "emacs-guix"
         <<guix-pkg>>
         ))))
#+end_src
* doom
#+begin_src elisp :tangle init.el
(doom!
 :completion  company vertico ;(ivy +fuzzy +prescient +childframe)
 :ui          nav-flash (modeline +light) ophints (popup +all +defaults) ligatures hydra
 vc-gutter window-select treemacs
 :editor      (evil +everywhere) fold lispy snippets (format +onsave) multiple-cursors word-wrap
 :emacs       dired electric vc ibuffer undo
 :term        eshell vterm
 :tools       editorconfig (eval +overlay) (lookup +docsets) (magit) pdf (pass +auth) direnv make tree-sitter lsp
 :checkers    syntax
 :lang        clojure data emacs-lisp
 (org +contacts +dragndrop +pandoc +present +hugo +roam2) beancount
 rest sh plantuml (python +pyenv +lsp) (web +html) yaml json
 :app         calendar telega (rss +org) torrent exwm irc
 :email       (mu4e +gmail)
 :config      (default +bindings +smartparens) literate)
#+end_src
* theme
#+begin_src elisp :tangle packages.el
(package! circadian)
(package! white-sand-theme)
;(package! kaolin-themes)
(package! doom-themes)
#+end_src

#+begin_src elisp
(use-package! white-sand-theme)
(defvar doom-theme-dark 'doom-one)
(setq doom-theme 'white-sand
      doom-font (font-spec :family "Hack" :size 20)
      doom-serif-font (font-spec :family "Hack")
      doom-unicode-font (font-spec :family "Hack"))

;; Change color theme based on day time
(use-package! circadian
  :custom
  (calendar-latitude 52.516667)
  (calendar-longitude 13.388889)
  (circadian-themes
   `((:sunrise . ,doom-theme)
     (:sunset . ,doom-theme-dark)))
  :config
  (circadian-setup))
#+end_src

* gpg
#+begin_src scheme :noweb-ref guix-pkg :tangle no :exports none
"emacs-pinentry"
#+end_src

#+begin_src elisp :tangle packages.el
(package! pinentry :built-in 'prefer)
#+end_src

#+begin_src elisp
(use-package! pinentry
  :config
  (pinentry-start))
#+end_src
* emacs screencast recording
#+begin_src elisp :tangle packages.el
(package! command-log-mode)
;(package! camcorder) autoloads fail because of names.el usage. todo: debug
#+end_src

#+begin_src elisp
(use-package! command-log-mode
  :commands command-log-mode)
;; (use-package! camcorder
;;   :commands camcorder-mode
;;   :hook (camcorder-mode-hook . clm/command-log-clear))
#+end_src

#+begin_src scheme :noweb-ref guix-pkg :tangle no :exports none
"recordmydesktop" "ffmpeg"
#+end_src
* miscellaneous
#+begin_src sh :tangle ~/.local/bin/locate :shebang #!/bin/sh
plocate -d /storage/storage.plocate $@
plocate -d /storage/2tb.plocate $@
plocate -d /storage/500gb.plocate $@
#+end_src

#+begin_src elisp
(setq +modeline-encoding nil
      +modeline-height 20)

;; C-h deletes character backwards
(define-key key-translation-map [?\C-h] [?\C-?])

(setq-default
 ;; russian layout on C-\
 default-input-method "russian-computer")
#+end_src

Debug icons in modeline, find out their names.
#+begin_src elisp :tangle no
(defadvice! +debug-modeline-format-icon (icon-set icon label &optional face &rest args)
  :override +modeline-format-icon
  (propertize (format "X %s" label) 'face face))
#+end_src
* buffers
#+begin_src elisp :tangle packages.el
(package! bufler)
#+end_src

#+begin_src elisp
(use-package! bufler
  :config
  (after! exwm
    (exwm-input-set-key (kbd "s-b") #'bufler-switch-buffer))

  (after! evil-collection
    (evil-collection-define-key 'normal 'bufler-list-mode-map
      (kbd "C-k") 'bufler-list-buffer-kill
      "r" 'bufler
      (kbd "RET") 'bufler-list-buffer-switch))

  :custom
  (bufler-groups
   (bufler-defgroups
     (group (mode-match "Telega" (rx bos "telega-chat")))
     (group
      (lambda (buffer)
        (and
         (funcall (mode-match "exwm-mode" (rx bos "exwm-mode")) buffer)
         "X11"))
      (group
       (lambda (buffer)
         (and
          (string= "qutebrowser"
                   (buffer-local-value 'exwm-class-name buffer))
          "WWW"))))
     (group (auto-project))
     (auto-directory))))
#+end_src
* org mode
- [ ] TODO: Replace linkmarks with bookmark+
#+begin_src elisp :tangle packages.el
(package! linkmarks :recipe (:host github :repo "dustinlacewell/linkmarks"))
(package! org-modern-indent :recipe (:host github :repo "jdtsmith/org-modern-indent"))
(package! org-modern)
#+end_src

#+begin_src elisp
(setq
 org-directory (expand-file-name "~/Sync/org/")
 org-src-window-setup 'current-window
 org-fold-catch-invisible-edits 'show-and-error)

(use-package! org-contacts
  :custom
  (org-contacts-files (list (expand-file-name "contacts.org" org-directory))))

(use-package! org-modern
  :hook (org-mode org-agenda-finalize))

(use-package! org-modern-indent
  :hook org-mode)

(use-package! linkmarks
  :custom
  (linkmarks-file (expand-file-name "links.org" org-directory))

  :config
  (after! exwm
    (exwm-input-set-key (kbd "s-g") #'linkmarks-select)))

(after! org-download
  (setq org-download-screenshot-method "flameshot gui --raw > %s"))

(load! "org-defuns")
#+end_src
** capture
#+begin_src shell :tangle ~/.local/bin/emacs-capture :shebang #!/bin/sh
emacsclient -F '((name . "doom-capture") (height . 10) (width . 80))' "$@"
#+end_src

#+begin_src python :tangle ~/.local/share/qutebrowser/userscripts/orgprotocol :shebang #!/usr/bin/env python3
import os
import subprocess
from urllib.parse import quote

subprocess.call(['emacs-capture',
  'org-protocol://capture?template=t&url={url}&title={title}&body={body}'.format(
    url   = quote(os.environ.get('QUTE_URL', '')),
    title = quote(os.environ.get('QUTE_TITLE', '')),
    body  = quote(os.environ.get('QUTE_SELECTED_TEXT', ''))
  )
])
#+end_src
** agenda
#+begin_src elisp :tangle packages.el
(package! german-holidays)
#+end_src

#+begin_src elisp
(use-package! german-holidays
  :custom
  (calendar-holidays holiday-german-BE-holidays))

(setq
 org-agenda-files
 (-map (lambda (el) (expand-file-name el org-directory))
       '("projects.org"
         "tickler.org")))
#+end_src

** roam
#+begin_src elisp
(after! org-roam
  (setq org-roam-db-node-include-function
        (lambda ()
          (not (member "ATTACH" (org-get-tags))))))
#+end_src
** elasticsearch
#+begin_src elisp :tangle no
(package! es-mode)
#+end_src

#+begin_src elisp :tangle no
(use-package! es-mode
  :commands (org-babel-execute:es)

  :config
  (appendq!
   +org-babel-mode-alist '((es . elasticsearch))))
#+end_src
* git
#+begin_src elisp :tangle packages.el
(package! magit-todos :disable t) ; too slow
#+end_src

#+begin_src elisp
(after! magit
  (let ((sarg-repos-dir (expand-file-name "~/devel/")))
    (setq magit-repository-directories `((,sarg-repos-dir . 2))
          magit-clone-default-directory `,sarg-repos-dir)))

(after! browse-at-remote
  (setq browse-at-remote-prefer-symbolic nil))
#+end_src
* tramp
#+begin_src elisp
(after! tramp
  (pushnew! tramp-remote-path 'tramp-own-remote-path))
#+end_src
* book reading
#+begin_src elisp :tangle packages.el
(package! calibredb :built-in 'prefer)
(package! nov :built-in 'prefer)
#+end_src

#+begin_src scheme :noweb-ref guix-pkg :tangle no :exports none
"emacs-calibredb" "sqlite" "emacs-nov-el"
#+end_src

#+begin_src elisp :tangle no
(use-package! nov
  :mode ("\\.epub\\'" . nov-mode)

  :custom
  (nov-variable-pitch nil)
  (nov-text-width t))
#+end_src

#+begin_src elisp
(use-package! calibredb
  :commands calibredb
  :config
  (setq calibredb-root-dir (expand-file-name "~/Calibre Library")
        calibredb-db-dir (concat calibredb-root-dir "/metadata.db")
        calibredb-library-alist '(("~/Calibre Library"))
        calibredb-preferred-format 'epub ;; just open it, don't ask

        ;; hide extra columns
        calibredb-date-width 0
        calibredb-format-width 0
        calibredb-tag-width 0
        calibredb-comment-width 0)

  (defalias 'calibredb-open-with-default-tool #'find-file))
#+end_src

* wpa-manager
#+begin_src elisp :tangle packages.el
(package! wpa-manager
  :recipe (:host github :repo "sarg/wpa-manager.el"))
#+end_src

#+begin_src elisp
(use-package! wpa-manager
  :init
  (after! evil-collection
    (evil-collection-inhibit-insert-state 'wpa-manager-mode-map)
    (evil-set-initial-state 'wpa-manager-mode-map 'normal)
    (evil-collection-define-key 'normal 'wpa-manager-mode-map
      "s" 'wpa-manager-scan
      "r" 'revert-buffer
      "c" 'wpa-manager-connect
      (kbd "RET") 'wpa-manager-connect)))
#+end_src
* password-store
#+begin_src elisp :tangle packages.el
(package! password-generator)
#+end_src

#+begin_src elisp
(setq password-cache-expiry (* 60 15))

;; for magithub auth to work create pass entry user^magithub@api.github.com
(after! magit
    (setq magit-process-find-password-functions '(magit-process-password-auth-source)))

(use-package! password-generator
  :after password-store

  :config
  (defadvice! +password-store-edit-locally (entry)
    :override #'password-store-edit
    (interactive (list (password-store--completing-read t)))
    (find-file (password-store--entry-to-file entry)))

  (defadvice! +password-store-generate-strong (entry &optional len)
    :override #'password-store-generate
    (interactive (list (password-store--completing-read)
                       (when current-prefix-arg
                         (abs (prefix-numeric-value current-prefix-arg)))))
    (let ((pass
           (password-generator-strong
            (or len password-store-password-length) t)))
      (password-store-insert entry pass))))
#+end_src

** qutebrowser integration
#+begin_src elisp
(defun +pass/qute (url)
  (auth-source-pass--read-entry
   (completing-read "Pass: "
                    (password-store-list)
                    nil t url)))
#+end_src

#+begin_src bash :tangle ~/.config/qutebrowser/password_fill_rc
init() { file="not used"; }
choose_entry() { true ; }
no_entries_found() { if [ 0 -eq "${#lines[@]}" ]; then exit 0; fi }

open_entry() {
    local firstline=1
    for line in "${lines[@]}"; do
        if ((firstline)) ; then
            password="$line"
            firstline=0
        else
            if [[ $line =~ $user_pattern ]] ; then
                # remove the matching prefix "user: " from the beginning of the line
                username=${line#${BASH_REMATCH[0]}}
                break
            fi
        fi
    done
}

query_entries() {
    url=$(awk -F'[./]' '{print $(NF-1) "." $(NF)}' <<< "$1")

    set +o errexit
    mapfile -t lines < <(emacsclient -e "(+pass/qute \"$url\")" | jq -r .)
    set -o errexit
}
#+end_src

* elfeed
#+begin_src elisp
(defun elfeed-open-special ()
  "Open videos directly."
  (interactive)
  (let ((entry (elfeed-search-selected :single)))
    (if (seq-contains-p (elfeed-entry-tags entry) 'youtube)
        (elfeed-search-browse-url)
      (elfeed-search-show-entry entry))))

(defun sarg/elfeed-strip-content (entry)
  (when (seq-contains-p (elfeed-entry-tags entry) 'rmbody)
    (setf (elfeed-entry-content entry) (elfeed-ref ""))
    (elfeed-untag entry 'rmbody)))

(after! elfeed
  (add-hook 'elfeed-new-entry-hook #'sarg/elfeed-strip-content)
  (add-hook 'elfeed-show-mode-hook (lambda () (setq-local browse-url-generic-program "qutebrowser-background")))

  (setq elfeed-search-filter "@2-week-ago +unread -youtube"
        elfeed-show-entry-switch (lambda (buf) (display-buffer-below-selected buf nil) (select-window (get-buffer-window buf))))

  (evil-define-key 'normal elfeed-search-mode-map
    (kbd "RET") 'elfeed-open-special))
#+end_src

#+begin_src sh :tangle ~/.local/bin/qutebrowser-background :shebang #!/bin/sh
qutebrowser --nowindow --target tab-bg-silent $@
#+end_src

* dict
#+begin_src elisp :tangle packages.el
(package! dictcc)
(package! multitran)
(package! google-translate)
(package! powerthesaurus)
#+end_src
* email
- [ ] TODO: fix mu4e popping up for mailto urls
#+begin_src desktop :tangle ~/.local/share/applications/emacsclient-mail.desktop
[Desktop Entry]
Categories=Network;Email;
Comment=GNU Emacs is an extensible, customizable text editor - and more
Exec=emacs-mail %u
Icon=emacs
Name=Emacs (Mail, Client)
MimeType=x-scheme-handler/mailto;
NoDisplay=true
Terminal=false
Type=Application
Keywords=emacsclient;
Actions=new-window;new-instance;
#+end_src
#+begin_src shell :tangle ~/.local/bin/emacs-mail :shebang #!/bin/sh
emacsclient --eval "(message-mailto \"$1\")"
#+end_src
#+begin_src elisp
(defun sarg/ensure-msmtp-pass-available ()
  (interactive)
  (auth-source-pass-get (concat "Email/" user-mail-address) 'secret))

(after! mu4e
  (setq mu4e-filter-inbox "flag:unread AND NOT flag:trashed AND maildir:/gmail/Inbox"
        mu4e-bookmarks '((mu4e-filter-inbox "Gmail messages" ?u)
                         ("date:today..now AND NOT flag:trashed AND NOT maildir:/gmail/trash" "Today's messages" 116))

        mu4e-alert-interesting-mail-query mu4e-filter-inbox)

  (add-to-list 'mm-body-charset-encoding-alist '(utf-8 . 8bit))
  (advice-add 'sendmail-send-it
              :before #'sarg/ensure-msmtp-pass-available)

  (setq sendmail-program (executable-find "msmtp")
        send-mail-function #'sendmail-send-it
        mu4e-compose-format-flowed t ; visual-line-mode + auto-fill upon sending
        message-sendmail-f-is-evil t
        message-sendmail-extra-arguments '("--read-envelope-from")
        message-send-mail-function #'message-send-mail-with-sendmail))

(set-email-account! "sarg"
  '((mu4e-sent-folder   . "/gmail/sent")
    (mu4e-drafts-folder . "/gmail/drafts")
    (mu4e-trash-folder  . "/gmail/trash")
    (mu4e-refile-folder . "/gmail/all")
    (smtpmail-smtp-user . "sarg@sarg.org.ru")
    (user-mail-address  . "sarg@sarg.org.ru"))
  t)
#+end_src

* browsing
#+begin_src elisp :tangle packages.el
(package! webpaste)
(package! web-search)
(package! mpv)
#+end_src

#+begin_src elisp
(defun browse-url-qute-private (url &rest _)
  "Make qutebrowser open URL in private-browsing window."
  (interactive (browse-url-interactive-arg "URL: "))
  (let ((process-environment (browse-url-process-environment)))
    (start-process "qutebrowser-private" nil
           "qutebrowser" "--target" "private-window" url)))

(defun browse-url-mpv (url &rest _)
  (mpv-play-url url))

(setq browse-url-handlers
      '(("youtu\\.?be" . browse-url-mpv)))

(setq-default
 browse-url-browser-function 'browse-url-generic
 browse-url-generic-program "qutebrowser")

(use-package! web-search
  :commands web-search
  :custom
  (web-search-default-provider "DuckDuckGo"))
#+end_src
* shell
#+begin_src elisp :tangle packages.el
(package! eat :built-in 'prefer)
(package! detached :built-in 'prefer)
#+end_src

#+begin_src scheme :noweb-ref guix-pkg :tangle no :exports none
"emacs-eat" "emacs-detached"
#+end_src

#+begin_src scheme :noweb-ref guix-srv :tangle no :exports none
(simple-service 'eat-bash-integration home-bash-service-type
 (home-bash-extension
  (bashrc (list
           (plain-file "eat"
            "[ -n \"$EAT_SHELL_INTEGRATION_DIR\" ] && source \"$EAT_SHELL_INTEGRATION_DIR/bash\"")))))
#+end_src

#+begin_src elisp
(after! eshell
  (add-hook 'eshell-first-time-mode-hook
            (lambda ()
              (map! :map eshell-mode-map
                    :ni "C-r" #'+eshell/search-history))))

(use-package! eat
  :hook (eshell-load-hook . (eat-eshell-mode eat-eshell-visual-command-mode)))

(use-package! detached
  :init
  (detached-init)
  :bind (;; Replace `async-shell-command' with `detached-shell-command'
         ([remap async-shell-command] . detached-shell-command)
         ;; Replace `compile' with `detached-compile'
         ([remap compile] . detached-compile)
         ([remap recompile] . detached-compile-recompile)
         ;; Replace built in completion of sessions with `consult'
         ([remap detached-open-session] . detached-consult-session))
  :custom ((detached-show-output-on-attach t)
           (detached-terminal-data-command system-type)))
#+end_src

* dired
#+begin_src elisp :tangle packages.el
(package! dired-filter :built-in 'prefer)
(package! dired-avfs :built-in 'prefer)
(package! dired-du :built-in 'prefer)
(package! dired-collapse)
(package! dired-git-info
  :recipe (:host github :repo "clemera/dired-git-info")
  :built-in 'prefer)
(package! openwith :built-in 'prefer)
#+end_src

#+begin_src scheme :noweb-ref guix-pkg :tangle no :exports none
"emacs-dired-hacks" "emacs-dired-du"
"emacs-dired-git-info" "emacs-openwith"
#+end_src

#+begin_src elisp
(use-package! dired-filter
  :hook dired-mode
  :custom
  (dired-filter-stack '((omit) (dot-files))))

(use-package! dired-collapse
  :hook dired-mode)

(use-package! dired-git-info
  :custom
  (dgi-commit-message-format "%cr\t%s"))

(use-package! dired-avfs)
(use-package! dired-du
  :custom
  ;; human readable
  (dired-du-size-format 't))

(add-hook! dired-mode #'dired-hide-details-mode)

(after! async
  (dired-async-mode 1))

(defun openwith-has-association (file)
  (-any? (lambda (oa) (string-match (car oa) file)) openwith-associations))

(use-package! openwith
  :hook (after-init . openwith-mode)
  :custom
  (openwith-associations
   '(("\\.\\(?:pdf\\|epub\\)\\'" "zathura" (file))
     ("\\.\\(?:mkv\\|webm\\|avi\\|mp4\\)\\'" "mpv" (file))))

  :config
  ;; Don't ask if file is too large when it'll be handled by openwith-mode.
  (advice-add 'abort-if-file-too-large :before-until
              (lambda (size op-type filename &rest args)
                (and (boundp 'openwith-mode) openwith-mode
                     (openwith-has-association filename)))))
#+end_src

* openscad
#+begin_src elisp :tangle no
(defun openscad-preview ()
  (interactive)
  (select-window (split-window-right))
  (start-process "openscad" nil "openscad" (buffer-file-name)))
#+end_src

* selfcontrol
#+begin_src elisp :tangle no
(defun sarg/ad-selfcontrol-time-restriction (orig-fun &rest args)
  (if (or (= 0 (mod
                (calendar-day-of-week (calendar-current-date))
                6)) ; 0 and 6 - Sunday and Saturday
          (> (nth 2 (decode-time)) 18))
      (apply orig-fun args)
    (message "It's not the time yet!")))

(defun sarg/ad-selfcontrol-otp-confirm (proc &rest args)
  "Asks to enter random string as confirmation before executing PROC."
  (interactive)
  (let* ((pass (format "%06x%06x%06x"
                       (random (expt 16 6))
                       (random (expt 16 6))
                       (random (expt 16 6))))

         (input (read-string (format "Enter %s if you wish to proceed: " pass))))

    (if (string= input pass)
        (apply proc args))))

(after! elfeed
  (advice-add 'elfeed-update :around #'sarg/ad-selfcontrol-otp-confirm)
  (advice-add '=rss :around #'sarg/ad-selfcontrol-otp-confirm)
  (advice-add 'elfeed-update :around #'sarg/selfcontrol-time-restriction))

(after! telega
  (advice-add 'ivy-telega-chat-with :around #'sarg/ad-selfcontrol-otp-confirm))
#+end_src
* bindings
#+begin_src elisp
(map!
 "C-x b" #'bufler-switch-buffer

 (:map dired-mode-map
  :n (kbd "DEL") #'dired-up-directory
  :n "K" #'dired-do-kill-lines)

 (:map image-mode-map
   :n "q" #'kill-current-buffer))
#+end_src
* exwm
EXWM config is too big, so it was extracted to a separate [[../.doom.d/modules/app/exwm][doom app]].

#+begin_src scheme :noweb-ref guix-pkg :tangle no
"emacs-exwm"
#+end_src

** bindings
#+begin_src elisp :tangle no :results value pp :wrap example elisp
(-map (lambda (el) (cons (key-description (car el)) (cdr el)))
      exwm-input-global-keys)
#+end_src

#+begin_example elisp
(("s-<tab>" . exwm-jump-to-last-exwm)
 ("s-1" lambda nil
  (interactive)
  (exwm-workspace-switch 0))
 ("s-2" lambda nil
  (interactive)
  (exwm-workspace-switch 1))
 ("s-3" lambda nil
  (interactive)
  (exwm-workspace-switch 2))
 ("s-4" lambda nil
  (interactive)
  (exwm-workspace-switch 3))
 ("s-5" lambda nil
  (interactive)
  (exwm-workspace-switch 4))
 ("s-6" lambda nil
  (interactive)
  (exwm-workspace-switch 5))
 ("s-7" lambda nil
  (interactive)
  (exwm-workspace-switch 6))
 ("s-8" lambda nil
  (interactive)
  (exwm-workspace-switch 7))
 ("s-9" lambda nil
  (interactive)
  (exwm-workspace-switch 8))
 ("<s-w>" . exwm-workspace-switch)
 ("s-f" . exwm-layout-toggle-fullscreen)
 ("s-r" . app-launcher-run-app)
 ("s-c" . kill-buffer-and-window)
 ("s-u" . winner-undo)
 ("S-s-U" . winner-redo)
 ("s-h" . evil-window-left)
 ("s-j" . evil-window-down)
 ("s-k" . evil-window-up)
 ("s-l" . evil-window-right)
 ("s-H" . evil-window-move-far-left)
 ("s-J" . evil-window-move-very-bottom)
 ("s-K" . evil-window-move-very-top)
 ("s-L" . evil-window-move-far-right)
 ("M-s-h" . shrink-window-horizontally)
 ("M-s-j" . shrink-window)
 ("M-s-k" . enlarge-window)
 ("M-s-l" . enlarge-window-horizontally)
 ("s-e" lambda nil
  (interactive)
  (sarg/run-or-raise "qutebrowser" "qutebrowser")
  (exwm-workspace-switch
   (exwm-workspace-name-to-index "brow")))
 ("s-<return>" . +eshell/here)
 ("S-s-<return>" . +vterm/here)
 ("s-<f12>" lambda nil
  (interactive)
  (start-process "flameshot" nil "flameshot" "gui"))
 ("s-<delete>" lambda nil
  (interactive)
  (start-process "lock" nil "lock.sh"))
 ("<XF86AudioRaiseVolume>" . pulseaudio-control-increase-sink-volume)
 ("<XF86AudioLowerVolume>" . pulseaudio-control-decrease-sink-volume)
 ("<XF86AudioMute>" . pulseaudio-control-toggle-current-sink-mute)
 ("<XF86AudioMicMute>" . pulseaudio-control-toggle-current-source-mute)
 ("<XF86MonBrightnessUp>" . backlight)
 ("<XF86MonBrightnessDown>" . backlight)
 ("s-b" . bufler-switch-buffer)
 ("s-g" . linkmarks-select))
#+end_example

* archive :noexport:
** emacs-one-frame
#+begin_src sh :tangle no :shebang #!/bin/bash
# https://taingram.org/2017/05/09/using-emacsclient-to-speed-up-editing/
# if no args open new frame
if [ $# -eq 0 ]; then
    emacsclient -c -n
    exit
fi

emacsclient -e "(frames-on-display-list \"$DISPLAY\")" &>/dev/null

if [ $? -eq 0 ]; then
    emacsclient -n "$*"
else
    emacsclient -c -n "$*"
fi
#+end_src
